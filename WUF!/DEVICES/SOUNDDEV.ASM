;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Generic Sound Device routines
;       Ignacio Mellado Bataller ( B52 / the DarkRising )
;
; NOTE: All functions assume that 'SearchSoundDevice' has been called first
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
.386p
.model flat
.code

        INCLUDE sb1xx.inc
		INCLUDE sb200.inc
		INCLUDE sb201.inc
		INCLUDE sb3xx.inc
		INCLUDE sb4xx.inc
        INCLUDE sounddev.inc

SOUND_DEVICES   EQU 5

; Devices will be scanned in left-right order
SoundDevices    dd offset SB4xxDevice, offset SB3xxDevice, offset SB201Device, offset SB200Device, offset SB1xxDevice

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Looks for an active Sound Device
;
; OUTPUT  : CF = 0 if sound device found ok
;           CF = 1 if no sound device available
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SearchSoundDevice       proc
        ; First, we try environment detection
        mov     ecx,SOUND_DEVICES
        xor     esi,esi
        env_detect:
                push    ecx esi
                mov     eax,[SoundDevices+esi*4]
                call    [eax.SDEnvDetection]
                pop     esi ecx
                jnc     found_device
                inc     esi
        loop    env_detect
        ; Now we try hardware detection
        mov     ecx,SOUND_DEVICES
        xor     esi,esi
        hard_detect:
                mov     eax,[SoundDevices+esi*4]
                cmp     [eax.SDHardDetection],0
                jz      no_hard_detect
                push    ecx esi
                call    [eax.SDHardDetection]
                pop     esi ecx
                jnc     found_device
                no_hard_detect:
                inc     esi
        loop    hard_detect
        ; Didn't find a sound device... <:'(
        stc
        ret

        found_device:
        mov     eax,[SoundDevices+esi*4]
        mov     SDev,eax
        clc
        ret
SearchSoundDevice       endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Opens the active Sound Device
;
; OUTPUT : CF = 0 if sound device opened ok
;          CF = 1 otherwise
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
OpenSoundDevice         proc
        mov     eax,SDev
        call    [eax.SDDeviceInit]
        ret
OpenSoundDevice         endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Closes the active Sound Device
;
; OUTPUT : CF = 0 if sound device closed ok
;          CF = 1 otherwise
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
CloseSoundDevice        proc
        mov     eax,SDev
        call    [eax.SDDeviceEnd]
        ret
CloseSoundDevice        endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Gets the name of the active sound device
;
; OUTPUT : EAX -> Sound device name
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
GetSoundDeviceName      proc
        mov     esi,SDev
        call	[esi.SDGetDeviceName]
        ret
GetSoundDeviceName      endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Gets the name of the sound driver
;
; OUTPUT : EAX -> Sound driver name
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
GetSoundDriverName		proc
		mov		esi,SDev
		call	[esi.SDGetDriverName]
		ret
GetSoundDriverName		endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Gets the address and size of the play and record buffers
;
; OUTPUT : EAX -> Playback buffer (SoundBuffer structure)
;          EBX -> Record buffer (SoundBuffer structure)
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
GetSDBuffers    proc
        mov     esi,SDev
        call    [esi.SDGetSoundBuffs]
        ret
GetSDBuffers    endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Starts playing from the playback buffer
;
; OUTPUT : CF = 0 if ok
;          CF = 1 if hardware doesn't answer
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SDPlay  proc
        mov     esi,SDev
        call    [esi.SDStartPlay]
        ret
SDPlay  endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Stops playing from the playback buffer
;
; OUTPUT : CF = 0 if ok
;          CF = 1 if hardware doesn't answer
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SDStopPlaying  proc
        mov     esi,SDev
        call    [esi.SDStopPlay]
        ret
SDStopPlaying  endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Gets playback properties
;
; OUTPUT : EAX -> Playback properties (SoundProperties structure)
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SDGetPlayProperties     proc
        mov     esi,SDev
        call    [esi.SDGetPlayProps]
        ret
SDGetPlayProperties     endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Sets playback properties
;
; INPUT  : EAX -> Playback properties (SoundProperties structure), or NULL
;
; OUTPUT : CF = 0 if ok
;          CF = 1 if hardware capabilities exceeded or device does not respond
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SDSetPlayProperties     proc
		; If NULL, leave
		or		eax,eax
		jz		sd_set_props_error
		; Check that the requested properties match a valid hardware configuration
		push	eax
		call	SDGetPlayHardCapabilities
		pop		ebx
		mov		esi,eax
		movzx	ecx,[esi.NumCapabilities]
		mov		eax,[esi.Capabilities]
		check_playback_capabilities:
				mov		edx,[ebx.SPSamplingRate]
				cmp		edx,[eax.SCMinSamplingRate]
				jb		next_playback_capability
				cmp		edx,[eax.SCMaxSamplingRate]
				ja		next_playback_capability
				mov		dl,[ebx.SPBitsPerSample]
				cmp		dl,[eax.SCBitsPerSample]
				jne		next_playback_capability
				mov		dl,[ebx.SPNumChannels]
				cmp		dl,[eax.SCNumChannels]
				jne		next_playback_capability
				jmp		found_playback_capability_match
				next_playback_capability:
				add		eax,size SoundCapability
		dec		ecx
		jnz		check_playback_capabilities
		sd_set_props_error:
		stc
		ret
		
		found_playback_capability_match:
		; The hardware can provide the requested properties: set them in the driver
		movzx	edx,[esi.NumCapabilities]
		sub		ecx,edx
		neg		ecx		; capability_index = -(loop_counter - total_capabilities)
        mov     esi,SDev
		mov		eax,ebx
		mov		ebx,ecx
        call    [esi.SDSetPlayProps]
        ret
SDSetPlayProperties     endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Gets recording properties
;
; OUTPUT : EAX -> Recording properties (SoundProperties structure)
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SDGetRecProperties      proc
        mov     esi,SDev
        call    [esi.SDGetRecProps]
        ret
SDGetRecProperties      endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Sets recording properties
;
; INPUT  : EAX -> Recording properties (SoundProperties structure), or NULL
;
; OUTPUT : CF = 0 if ok
;          CF = 1 if hardware capabilities exceeded or device does not respond
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SDSetRecProperties      proc
        mov     esi,SDev
        call    [esi.SDSetRecProps]
        ret
SDSetRecProperties      endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Gets sound device hardware playback capabilities
;
; OUTPUT : EAX -> Playback capabilities (SoundHardwareCapabilities structure)
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SDGetPlayHardCapabilities   proc
        mov     esi,SDev
        call    [esi.SDGetPlayHardCaps]
        ret        
SDGetPlayHardCapabilities   endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Gets sound device hardware recording capabilities
;
; OUTPUT : EAX -> Recording capabilities (SoundHardwareCapabilities structure)
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SDGetRecHardCapabilities	proc
		mov		esi,SDev
		call	[esi.SDGetRecHardCaps]
		ret
SDGetRecHardCapabilities	endp

.data?
SDev            dd ?

end
