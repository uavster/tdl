;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;  [tDR], 1999                                    Copyright the DarkRising
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;                                      Coded by:             B52 & Nitro!
;  SLI routines
;
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;  DATE         REVISION           AUTHOR & COMMENTS
;  컴컴컴컴     컴컴컴컴컴컴      컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;  18-5-99      Version 1.0        Nitro! : Comienza la comida de tarro
;  20-5-99      1.0 cont.          B52 : Sigue el papeo de olla
;
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
    .386p
    .model  flat
    jumps
    .code
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;  Needed includes
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
    include     newsgl.inc
    include     dpmi.inc
    include     utils.inc
    include     alloc.inc
    include     memcpy.inc
    include     stderror.inc
    include     sli.inc
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; AllocateSLI:         aloja una estructura SLI
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        EAX = N즡ero de frames
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         CF = 0 si ok
;                              EAX = NULL
;                              EBX = puntero al SLI vac죓
;                      CF = 1 si error
;                              EAX = error code
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
AllocateSLI     proc
                lea     ecx,[size SLI+eax*4]
                push    eax
                call    malloc
                pop     ecx
                ErrorCode MALLOC_ERROR
                lea     eax,[ebx+size SLI]
                mov     [ebx.SLIPImages],eax
                shl     ecx,16
                mov     [ebx.SLITotalFrames],ecx
                xor     eax,eax
                ret
                endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Comprueba que el n즡ero de bits del SLI es correcto y lo convierte a bytes
;
; INPUT  : ECX = N즡ero de bits
;
; OUTPUT : CF = 0 si ok
;               ECX = N즡ero de bytes correspondiente
;          CF = 1 si error
;               EAX = Error code (INVALID_COLOR_DEPTH)
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
NormalizeBits   proc
        cmp     ecx,8
        jz      bits_ok
        cmp     ecx,16
        jz      bits_ok
        cmp     ecx,24
        jz      bits_ok
        cmp     ecx,32
        jz      bits_ok
        cmp     ecx,32*3 ; para tablas  !!!!!!!!!!!!!!!!!!!!!!!!!!!!
        jz      bits_ok  ; !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
        mov     eax,INVALID_COLOR_DEPTH
        stc
        ret
        bits_ok:
        shr     ecx,3
        clc
        ret
NormalizeBits   endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Sets size and default clip
;
; INPUT  : EAX = X size
;          EBX = Y size
;          ECX = Color bytes
;          EDX = Number of frames
;          EDI -> SLI
;
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetDefProperties        proc
        mov     [SLI ptr edi.SLIXSize], eax
        mov     [SLI ptr edi.SLIYSize], ebx
        mov     [SLI ptr edi.SLIPitch], eax
        shl     edx,16
        mov     [SLI ptr edi.SLITotalFrames], edx
        shr     edx,16
        mov     [SLI ptr edi.SLIColorBits], ecx
        mov     [edi.SLIPos.SLRR1.SLPX],0
        mov     [edi.SLIPos.SLRR1.SLPY],0
        mov     [edi.SLIClip.SLRR1.SLPX],0
        mov     [edi.SLIClip.SLRR1.SLPY],0
        dec     eax
        dec     ebx
        mov     [edi.SLIPos.SLRR2.SLPX],eax
        mov     [edi.SLIPos.SLRR2.SLPY],ebx
        mov     [edi.SLIClip.SLRR2.SLPX],eax
        mov     [edi.SLIClip.SLRR2.SLPY],ebx
        inc     eax
        inc     ebx
        mov     [SLI ptr edi.SLIRef.SLPX], 0
        mov     [SLI ptr edi.SLIRef.SLPY], 0
        mov     [SLI ptr edi.SLIBlitType], SLIBlitCopy
        mov     [SLI ptr edi.SLIAlpha], 0
        mov     [SLI ptr edi.SLIMask], 0
        mov     [SLI ptr edi.SLIFrame], 0
        mov     [SLI ptr edi.SLIFrameRate], 1 SHL 16
        mov     [SLI ptr edi.SLILitTable], 0
        ret
SetDefProperties        endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Pone la lista de punteros a frame
;
; INPUT  : EAX = X size
;          EBX = Y size
;          ECX = Bytes per pixel
;          EDX = Frames
;          EDI -> SLI
;
; OUTPUT : ESI -> First frame
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetFramePointers        proc
        test    edx,edx
        jz      no_frames
        ; Need to set frame pointers
        push    eax ebx ecx edx
        lea     esi,[edi+size SLI]
        mov     [edi.SLIPImages],esi
        mov     ebp,edx
        mul     ebx
        mul     ecx
        mov     ecx,eax
        lea     edx,[esi+ebp*4]
        mov     [edi.SLIFramePtr],edx
        xor     ebx,ebx
        SetFramePtrs:
                mov     [esi+ebx*4],edx
                inc     ebx
                add     edx,ecx
        dec     ebp
        jnz     SetFramePtrs
        pop     edx ecx ebx eax
        mov     esi,[edi.SLIFramePtr]
        no_frames:
        ret
SetFramePointers        endp

;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; CreateSLI:           aloja una animaci줻 vacia en SLI
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        EAX : Size X
;                      EBX : Size Y
;                      ECX : ColorBits
;                      EDX : Number of frames
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         EBX = puntero al SLI
;                      EAX = NULL
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
CreateSLI       proc
                push    ebp
                call    NormalizeBits
                ErrorCodePOP eax, ebp
                push    eax ebx ecx edx
                mul     edx
                mul     ebx
                mul     ecx

                mov     ecx, eax
                add     ecx, size SLI
                pop     eax
                push    eax
                shl     eax,2
                add     ecx,eax         ; + size LISTA_PUNTEROS_FRAME
                call    malloc
                ErrorCodePOP MALLOC_ERROR, ebp
                mov     edi, ebx

                pop     edx ecx ebx eax

                call    SetDefProperties

                call    SetFramePointers

                push    edi

                mov     edi, esi
                mul     edx
                mul     ebx
                mul     ecx
                mov     ecx, eax
                mov     ebx, ecx
                shr     ecx, 2
                xor     eax, eax
                rep     stosd
                mov     ecx, ebx        ; por si acaso no es m즠tiplo de 4
                and     ecx,3
                or      ecx,ecx
                jz      no_stosb
                rep     stosb
                no_stosb:

                pop     ebx
                xor     eax,eax
                pop     ebp
                clc
                ret
                endp

;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; CreateVoidSLI:       aloja una animaci줻 vacia en SLI sin buffer
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        EAX : Size X
;                      EBX : Size Y
;                      ECX : ColorBits
;                      EDX : Number of frames
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         EBX = puntero al SLI
;                      EAX = NULL
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
CreateVoidSLI   proc
                call    NormalizeBits
                ErrorCode eax
                push    eax ebx ecx edx
                mov     eax,edx
                call    AllocateSLI
                mov     edi,ebx
                pop     edx ecx ebx eax
                ErrorCode edi
                mov     [SLI ptr edi.SLIFramePtr], 0

                call    SetDefProperties

                mov     ebx,edi
                xor     eax,eax
                clc
                ret
                endp

;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; DestroySLI:          libera un SLI de memoria
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        EAX : puntero al SLI
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         CF = 0 if ok
;                              EAX = NULL
;                      CF = 1 if error
;                              EAX = Error Code
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
DestroySLI      proc
                mov     ebx, eax
                call    free
                ErrorCode FREE_ERROR
                xor     eax,eax
                clc
                ret
                endp

;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; CreateVideoSLI:      Crea la superficie de video SLI
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        EAX : Size X
;                      EBX : Size Y
;                      ECX : ColorBits
;      Nota:           Si ECX+SGL_MemoryBuffer se fuerza a un buffer en
;                      memoria principal
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         CF = 0 if ok
;                              EAX = NULL
;                      CF = 1 if error
;                              EAX = Error Code
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴

.data?
VideoSLI        dd ?

.code
CreateVideoSLI  proc
                push    ebp
                push    eax ebx ecx
                call    SetVideoMode
                pop     ecx ebx eax
                ErrorCodePOP VIDEO_ERROR, ebp
                and     ecx,0ffffh
                call    NormalizeBits
                ErrorCodePOP eax, ebp
                push    eax ebx ecx
                mov     eax,1   ; 1 frame
                call    AllocateSLI
                mov     edi,ebx
                pop     ecx ebx eax
                ErrorCodePOP edi, ebp

                mov     edx,1
                call    SetDefProperties

                call    GetAvailPage
                mov     [SLI ptr edi.SLIFramePtr], eax

                mov     VideoSLI,edi
                xor     eax,eax
                pop     ebp
                clc
                ret
                endp
;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; GetVideoSLI:         Obtiene la superficie de video SLI
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        nada.
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         EAX : ptr to Video SLI
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
GetVideoSLI     proc
                call    GetAvailPage
                mov     edi, VideoSLI
                mov     [SLI ptr edi.SLIFramePtr], eax
                mov     eax, edi
                ret
                endp

;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; DestroyVIdeoSLI:          desaloja el VideoSLI
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        EAX <> 0 modo de video a seleccionar
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         CF = 0 if ok
;                              EAX = NULL
;                      CF = 1 if error
;                              EAX = Error Code
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
DestroyVideoSLI proc
                push    ebp
                call    UnsetVideoMode
                mov     ebx, VideoSLI
                call    free
                pop     ebp
                ErrorCode FREE_ERROR
                xor     eax,eax
                clc
                ret
                endp

;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; ShowVideoSLI:        Muestra la superficie de video SLI
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        nada.
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         nada.
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
ShowVideoSLI    proc
                push    ebp
                call    ShowPage
                call    GetAvailPage
                mov     edi, VideoSLI
                mov     [SLI ptr edi.SLIFramePtr], eax
                pop     ebp
                ret
                endp

;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; SetPalette:          Pone el puntero a una paleta B:G:R:0
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        EAX -> SLI
;                      EBX -> Palette
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         nada.
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetPalette      proc
        test    ebx,ebx
        jz      no_palette
        mov     esi,ebx
        lea     edi,[eax.SLIPalette]
        mov     ecx,256
        rep     movsd
        no_palette:
        ret
        endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Devuelve el n즡ero de frame actual
;
; INPUT  : EAX -> SLI
;
; OUTPUT : EAX = Frame
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
GetFrame        proc
        mov     eax,[eax.SLIFrame]
        shr     eax,16
        ret
GetFrame        endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Pone el n즡ero de frame actual
;
; INPUT  : EAX -> SLI
;          EBX = Frame
;
; OUTPUT : CF = 0 si ok
;               EAX = NULL
;          CF = 1 si error
;               EAX = Error code (INVALID_FRAME)
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetFrame        proc
        mov     ecx,[eax.SLITotalFrames]
        shr     ecx,16
        cmp     ebx,ecx
        jae     setframe_error

        mov     ecx,[eax.SLIPImages]
        mov     edx,[ecx+ebx*4]
        mov     [eax.SLIFramePtr],edx
        shl     ebx,16
        mov     [eax.SLIFrame],ebx
        xor     eax,eax
        clc
        ret

        setframe_error:
        mov     eax,INVALID_FRAME
        stc
        ret
        endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Devuelve el puntero a un frame determinado
;
; INPUT  : EAX -> SLI
;          EBX = Frame
;
; OUTPUT : CF = 0 si ok
;               EAX = NULL
;               EBX -> Frame
;          CF = 1 si el frame no existe
;               EAX = Error code
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
GetFramePtr     proc
        mov     ecx,[eax.SLITotalFrames]
        shr     ecx,16
        cmp     ebx,ecx
        jae     getframe_error
        mov     ecx,[eax.SLIPImages]
        mov     ebx,[ecx+ebx*4]
        clc
        ret

        getframe_error:
        mov     eax,INVALID_FRAME
        stc
        ret
GetFramePtr     endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Pone el puntero a un frame determinado
;
; INPUT  : EAX -> SLI
;          EBX = N즡ero de frame
;          ECX -> Frame
;
; OUTPUT : CF = 0 si ok
;               EAX = NULL
;          CF = 1 si el frame no existe
;               EAX = Error code
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetFramePtr     proc
        mov     edx,[eax.SLITotalFrames]
        shr     edx,16
        cmp     ebx,edx
        jae     setframeptr_error
        mov     edx,[eax.SLIPImages]
        mov     [edx+ebx*4],ecx
        clc
        ret

        setframeptr_error:
        mov     eax,INVALID_FRAME
        stc
        ret
SetFramePtr     endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Devuelve el puntero al frame actual
;
; INPUT  : EAX -> SLI
;
; OUTPUT : EAX -> Frame actual
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
GetCurrentFramePtr      proc
        mov     eax,[eax.SLIFramePtr]
        ret
GetCurrentFramePtr      endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Pone el color de m쟳cara
;
; INPUT : EAX -> SLI
;         EBX = Color de m쟳cara en formato B:G:R:0
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetMaskColor    proc
        mov     [eax.SLIMask],ebx
        ret
SetMaskColor    endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Pone el m굏odo de blitting
;
; INPUT : EAX -> SLI
;         EBX = Tipo de blit ('SLIBlitCopy', 'SLIBlitAlpha', 'SLIBlitAlphaMap'
;                             y los mismos con '+ SLIMaskedColor' para poner
;                             con m쟳cara)
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetBlitType     proc
        mov     [eax.SLIBlitType],ebx
        ret
SetBlitType     endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Pone el rect쟮gulo de clipping
;
; INTPUT : EAX = x1
;          EBX = y1
;          ECX = x2
;          EDX = y2
;          EDI -> SLI
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetClip         proc
        mov     [edi.SLIClip.SLRR1.SLPX],eax
        mov     [edi.SLIClip.SLRR1.SLPY],ebx
        mov     [edi.SLIClip.SLRR2.SLPX],ecx
        mov     [edi.SLIClip.SLRR2.SLPY],edx
        ret
SetClip         endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Desplaza el rect쟮gulo de destino al punto especificado
;
; INPUT : EAX = x1
;         EBX = y1
;         EDI -> SLI
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetPos         proc
        mov     ecx,[edi.SLIPos.SLRR2.SLPX]
        sub     ecx,[edi.SLIPos.SLRR1.SLPX]
        mov     edx,[edi.SLIPos.SLRR2.SLPY]
        sub     edx,[edi.SLIPos.SLRR1.SLPY]
        mov     [edi.SLIPos.SLRR1.SLPX],eax
        mov     [edi.SLIPos.SLRR1.SLPY],ebx
        add     eax,ecx
        add     ebx,edx
        mov     [edi.SLIPos.SLRR2.SLPX],eax
        mov     [edi.SLIPos.SLRR2.SLPY],ebx
        ret
SetPos         endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Devuelve las dimensiones (con escalado) del SLI
;
; INPUT  : EAX -> SLI
;
; OUTPUT : EAX = X size
;          EBX = Y size
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
GetDimensions   proc
        mov     ecx,eax
        mov     eax,[ecx.SLIPos.SLRR2.SLPX]
        sub     eax,[ecx.SLIPos.SLRR1.SLPX]
        mov     ebx,[ecx.SLIPos.SLRR2.SLPY]
        sub     ebx,[ecx.SLIPos.SLRR1.SLPY]
        inc     eax
        inc     ebx
        ret
GetDimensions   endp

;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; Pone el campo de alpha
;
; INPUT : EAX -> SLI
;         EBX = Alpha constante o puntero al mapa de alphas
;컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
SetAlpha        proc
        mov     [eax.SLIAlpha],ebx
        ret
SetAlpha        endp

;旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
; ClearCurrentFrame:   Borra el frame actual al color dado
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Entrada:        EAX -> SLI
;                      EBX -> color en el formato del SLI
;쳐컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
;      Salida:         nada.
;읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴
ClearCurrentFrame       proc
        mov     ecx, [SLI ptr eax.SLIColorBits]
        mov     edi, [SLI ptr eax.SLIFramePtr]
        mov     esi, [SLI ptr eax.SLIYSize]
        imul    esi, [SLI ptr eax.SLIXSize]

        cmp     ecx, 3
        jne     CCF000

        mov     edx, ebx
        shr     edx, 16
    CCF003:
        mov     [edi], bx
        mov     [edi+2], dl
        add     edi, 3
        dec     esi
        jnz     CCF003
        ret
    CCF000:
        cmp     ecx, 1
        jne     CCF001
        mov     eax, ebx
        shl     eax, 8
        mov     al, bl
        shl     eax, 8
        mov     al, bl
        shl     eax, 8
        mov     al, bl

        mov     ecx, esi
        push    ecx
        shr     ecx, 2
        rep     stosd
        pop     ecx
        and     ecx, 11b
        or      ecx, ecx
        jz      CCF004
        rep     stosb
    CCF004:
        ret

    CCF001:
        cmp     ecx, 2
        jne     CCF002
        mov     eax, ebx
        shl     eax, 16
        mov     ax, bx

        mov     ecx, esi
        push    ecx
        shr     ecx, 1
        rep     stosd
        pop     ecx
        and     ecx, 1b
        or      ecx, ecx
        jz      CCF005
        rep     stosw
    CCF005:
        ret
    CCF002:
        mov     eax, ebx
        mov     ecx, esi
        rep     stosd
        ret
        endp

END
